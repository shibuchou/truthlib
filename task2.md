# 1.学习第二章节内容编写代码
代码：   
;主引导程序   
;------------------------------------------------------------    
 SECTION MBR vstart=0x7c00   
 mov ax,cs   
 mov ds,ax   
 mov es,ax   
 mov ss,ax   
 mov fs,ax    
 mov sp,0x7c00    
 
; 清屏利用 0x06 号功能,上卷全部行,则可清屏｡    
; -----------------------------------------------------------     
 ;INT 0x10 功能号:0x06 功能描述:上卷窗口  
;------------------------------------------------------     
 ;输入:   
 ;AH 功能号= 0x06   
 ;AL = 上卷的行数(如果为 0,表示全部)   
 ;BH = 上卷行属性  
 ;(CL,CH) = 窗口左上角的(X,Y)位置  
 ;(DL,DH) = 窗口右下角的(X,Y)位置  
 ;无返回值:   
 mov ax, 0x600  
 mov bx, 0x700  
 mov cx, 0 ; 左上角: (0, 0)  
 mov dx, 0x184f ; 右下角: (80,25),  
 ; VGA 文本模式中,一行只能容纳 80 个字符,共 25 行｡ 
 ; 下标从 0 开始,所以 0x18=24,0x4f=79   
 int 0x10 ; int 0x10   
 
 ;;;;;;;;; 下面这三行代码获取光标位置 ;;;;;;;;;   
 ;.get_cursor 获取当前光标位置,在光标位置处打印字符｡  
 mov ah, 3 ; 输入: 3 号子功能是获取光标位置,需要存入 ah 寄存器  
 mov bh, 0 ; bh 寄存器存储的是待获取光标的页号  
 
 int 0x10 ; 输出: ch=光标开始行,cl=光标结束行  
 ; dh=光标所在行号,dl=光标所在列号  
 
 ;;;;;;;;; 获取光标位置结束 ;;;;;;;;;;;;;;;;   
 
 ;;;;;;;;; 打印字符串 ;;;;;;;;;;;   
 ;还是用 10h 中断,不过这次调用 13 号子功能打印字符串  
 mov ax, message   
 mov bp, ax ; es:bp 为串首地址,es 此时同 cs 一致,   
 ; 开头时已经为 sreg 初始化  
 
 ; 光标位置要用到 dx 寄存器中内容,cx 中的光标位置可忽略  
 mov cx, 5 ; cx 为串长度,不包括结束符 0 的字符个数  
 mov ax, 0x1301 ;子功能号 13 显示字符及属性,要存入 ah 寄存器,   
 ; al 设置写字符方式 ah=01: 显示字符串,光标跟随移动  
 mov bx, 0x2 ; bh 存储要显示的页号,此处是第 0 页,    
 ; bl 中是字符属性,属性黑底绿字(bl = 02h)   
 int 0x10 ; 执行 BIOS 0x10 号中断  
 ;;;;;;;;; 打字字符串结束 ;;;;;;;;;;;;;;;    

 jmp $ ; 使程序悬停在此   

 message db "1 MBR"   
 times 510-($-$$) db 0    
 db 0x55,0xaa  
 ##### 进行编译
 nasm -o mbr mbr.S

 dd if=/home/shibuchou/truth/mbr.bin of=/home/shibuchou/truth/bochs/hd60M.img bs=512 count=1 conv=notrunc  
 ![结果](https://github.com/shibuchou/truthlib/blob/51d83be12d8bfc615c449cbe7f6b3649d6b4e338/%E5%BE%AE%E4%BF%A1%E5%9B%BE%E7%89%87_20250124223145.png)
 
# 2.学习基础的汇编指令
## bios是什么
BIOS 全称叫 Base Input & Output System，即基本输入输出系统。  
在开机的一瞬间，也就是接电的一瞬间，CPU 的 cs：ip 寄存器被强制初始化为 0xF000：0xFFF0。此时BIOS启动  
## mbr是什么
MBR（主引导记录） 所在的位置是磁盘上最开始的那个扇区，0 盘 0 道 1 扇区  
大小必须为512byte多余的补0，最后两字节必须是0x55和0xaa
## 分支跳转指令
## 思考mbr与bios之间的关系是什么？为什么需要mbr？mbr在整个操作系统启动阶段中的作用是什么？
mbr是BIOS自检时需要调用的第一个程序，BIOS将MBR中的引导程序加载到内存并执行，从而将控制权交给MBR。  
MBR中包含分区表，记录了硬盘的分区信息（如分区的起始位置、大小等），操作系统通过这些信息识别硬盘的分区结构  
在BIOS完成硬件初始化后，MBR中的引导程序被加载到内存并执行，MBR的引导程序会检查分区表的有效性，确保分区信息正确。  
